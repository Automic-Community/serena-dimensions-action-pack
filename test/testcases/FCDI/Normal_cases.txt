*** Settings ***
Default Tags      win
Test Template     FCDI Fetch (Get) Request Items
Resource          ../../resources/keywords.txt

*** Variables ***
${SPACE}          " "

*** Test Cases ***    USERNAME                               PASSWORD               HOST                                   DN NAME                                   DSN                                   REQUEST                         FOLDER                 ARGUMENTS                                             RETCODE    STATUS
1.Successful with empty arguments is empty
                      ${_DIMENSIONS_USER}                    ${_DIMENSIONS_PASS}    ${_DIMENSIONS_HOST}                    ${_DIMENSIONS_DB_NAME}                    ${_DIMENSIONS_DSN}                    QLARIUS_CR_2                    Qlarius Underwriter    ${EMPTY}                                              0          ${ENDED_OK}

2.Successful with empty arguments is not empty
                      ${_DIMENSIONS_USER}                    ${_DIMENSIONS_PASS}    ${_DIMENSIONS_HOST}                    ${_DIMENSIONS_DB_NAME}                    ${_DIMENSIONS_DSN}                    QLARIUS_CR_2                    Qlarius Underwriter    /LOGFILE="C:\\temp\\hdt\\FCDI.log"                    0          ${ENDED_OK}

3.Successful with parameters contain spaces
                      ${SPACE}${_DIMENSIONS_USER}${SPACE}    ${_DIMENSIONS_PASS}    ${SPACE}${_DIMENSIONS_HOST}${SPACE}    ${SPACE}${_DIMENSIONS_DB_NAME}${SPACE}    ${SPACE}${_DIMENSIONS_DSN}${SPACE}    ${SPACE}QLARIUS_CR_2${SPACE}    Qlarius Underwriter    ${SPACE}/LOGFILE="C:\\temp\\hdt\\FCDI.log"${SPACE}    0          ${ENDED_OK}

*** Keywords ***
FCDI Fetch (Get) Request Items
    [Arguments]    ${user}    ${pass}    ${host}    ${dbname}    ${dsn}    ${request}
    ...    ${folder}    ${args}    ${returnCode}    ${returnStatus}
    [Documentation]    This template used for testing action PCK.AUTOMIC_DIMENSIONS.PUB.ACTION.FCDI with inputs
    ...    - ${user}: Username to access Serena Dimensions CM.
    ...    - ${pass}: Password to access Serena Dimensions CM.
    ...    - ${host}: Serena Dimensions CM hostname
    ...    - ${dbname}: Specifies the Serena Dimensions CM database identifier.
    ...    - ${dsn}: message should be contained in execution report
    ...    - ${request}: The name of a Dimensions request.
    ...    - ${status}: Specifies the status to which the request is to be actioned. If this is not specified, the next default state is used.
    ...    - ${folder}: Location in which items are fetched into.
    ...    - ${args}: Optional Arguments, can be used to specify any optional arguments for FCDI.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    Init Action    ${ACTION_FCDI}
    Action Set    ${PRT_DIMENSIONS_USERNAME}    ${user}
    Action Set    ${PRT_DIMENSIONS_PASSWORD}    ${pass}
    Action Set    ${PRT_DIMENSIONS_HOST}    ${host}
    Action Set    ${PRT_DIMENSIONS_DBNAME}    ${dbname}
    Action Set    ${PRT_DIMENSIONS_DSN}    ${dsn}
    Action Set    ${PRT_FCDI_DIMENSIONS_REQUEST_ID}    ${request}
    Action Set    ${PRT_FCDI_DIMENSIONS_FOLDER}    ${folder}
    Action Set    ${PRT_FCDI_DIMENSIONS_ARGS}    ${args}
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
