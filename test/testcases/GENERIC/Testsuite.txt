*** Settings ***
Default Tags      win
Test Template     Generic Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK    USERNAME                       PASSWORD                       HOST                           DN NAME                           DSN                           COMMAND    ARGUMENTS                 RETCODE    STATUS          REPORT MESSAGE
1.Successful execute a command & empty argument
                      OK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            LWS        ${EMPTY}                  0          ${ENDED_OK}     List of projects currently in use

2.Successful execute a command & argument
                      OK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            LWS        /FILENAME=worklist.txt    0          ${ENDED_OK}     Writing report to worklist.txt

3.Fail with invalid user
                      NOK       ${_DIMENSIONS_USER}_invalid    ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            LWS        ${EMPTY}                  0          ${ENDED_NOK}    ${EMPTY}

4.Fail with invalid password
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}_invalid    ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            LWS        ${EMPTY}                  0          ${ENDED_NOK}    ${EMPTY}

5.Fail with invalid host
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}_invalid    ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            LWS        ${EMPTY}                  0          ${ENDED_NOK}    ${EMPTY}

6.Fail with invalid database name
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}_invalid    ${_DIMENSIONS_DSN}            LWS        ${EMPTY}                  0          ${ENDED_NOK}    ${EMPTY}

7.Fail with invalid dsn
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}_invalid    LWS        ${EMPTY}                  0          ${ENDED_NOK}    ${EMPTY}

*** Keywords ***
Generic Template
    [Arguments]    ${OK_NOK}    ${user}    ${pass}    ${host}    ${dbname}    ${dsn}
    ...    ${command}    ${args}    ${returnCode}    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_DIMENSIONS.PUB.ACTION.GENERIC with inputs
    ...    - ${user}: Username to access Serena Dimensions CM.
    ...    - ${pass}: Password to access Serena Dimensions CM.
    ...    - ${host}: Serena Dimensions CM hostname
    ...    - ${dbname}: Specifies the Serena Dimensions CM database identifier.
    ...    - ${dsn}: Specifies the data source name for connecting to the database.
    ...    - ${command}: Specifies the data source name for connecting to the database.
    ...    - ${args}: Specifies any required arguments for the DMCLI command.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_GENERIC}
    Action Set    ${PRT_DIMENSIONS_USERNAME}    ${user}
    Action Set    ${PRT_DIMENSIONS_PASSWORD}    ${pass}
    Action Set    ${PRT_DIMENSIONS_HOST}    ${host}
    Action Set    ${PRT_DIMENSIONS_DBNAME}    ${dbname}
    Action Set    ${PRT_DIMENSIONS_DSN}    ${dsn}
    Action Set    ${PRT_GENERIC_DIMENSIONS_COMMAND}    ${command}
    Action Set    ${PRT_GENERIC_DIMENSIONS_ARGUMENTS}    ${args}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}
