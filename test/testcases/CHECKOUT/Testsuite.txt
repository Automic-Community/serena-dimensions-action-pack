*** Settings ***
Documentation     In order to execute this test, ${_CVS_BASE_MODULE} should be available and valid
Suite Setup       Setup CHECKOUT suite
Default Tags      win    unix
Test Template     Checkout Template
Resource          ../../resources/keywords.txt

*** Variables ***
${checkout_working_dir}    ${_WORKING_DIR}${/}CHECKOUT_TEST
${checkout_test_module}    ${_CVS_BASE_MODULE}

*** Test Cases ***    OK_NOK    URI                    MODULE                              PASSWORD                EXPORTPATH        REVISION       RETCODE    STATUS          REPORT MESSAGE
1.Check Out Success With Pserver & No Revision
                      OK        ${_CVS_URI}            ${checkout_test_module}             ${_CVS_PASS}            TC01              ${EMPTY}       0          ${ENDED_OK}     cvs checkout: Updating ${checkout_test_module}

2.Check Out Success With Revision
                      OK        ${_CVS_URI}            ${checkout_test_module}/CHECKOUT    ${_CVS_PASS}            TC02              1.1            0          ${ENDED_OK}     cvs checkout: Updating ${checkout_test_module}/CHECKOUT

3.Fail with invalid URI
                      NOK       ${_CVS_URI}_invalid    ${checkout_test_module}             ${_CVS_PASS}            TC03              ${EMPTY}       0          ${ENDED_NOK}    no such repository

4.Fail with invalid Module
                      NOK       ${_CVS_URI}            ${checkout_test_module}_invalid     ${_CVS_PASS}            TC04              ${EMPTY}       0          ${ENDED_NOK}    cannot find module

5.Fail with invalid Password
                      NOK       ${_CVS_URI}            ${checkout_test_module}             ${_CVS_PASS}_invalid    TC05              ${EMPTY}       0          ${ENDED_NOK}    authorization failed

6.Fail with invalid Revision
                      NOK       ${_CVS_URI}            ${checkout_test_module}             ${_CVS_PASS}            TC06              1.1_invalid    0          ${ENDED_NOK}    Numeric tag 1.1_invalid contains characters other than digits and '.'

7. Success checkout to path contains space
                      OK        ${_CVS_URI}            ${checkout_test_module}             ${_CVS_PASS}            TC07 and space    ${EMPTY}       0          ${ENDED_OK}     cvs checkout: Updating ${checkout_test_module}

*** Keywords ***
Setup CHECKOUT suite
    [Documentation]    Setup steps before execute test
    Log    Setup for testing CHECKOUT action
    Log    Try to delete created folder from last execution
    Delete Dir    ${checkout_working_dir}
    Create Dir    ${checkout_working_dir}
    Log    Finish setup for CHECKOUT testsuite

Checkout Template
    [Arguments]    ${OK_NOK}    ${uri}    ${module}    ${password}    ${exportPath}    ${revision}
    ...    ${returnCode}    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_CVS.PUB.ACTION.CHECKOUT with inputs
    ...    - ${uri}: Repository URI. Example: ":pserver:username@cvs.server.local:/usr/local/cvs".
    ...    - ${module}: Module that should be exported.
    ...    - ${password}: Password used for authentication with the repository.
    ...    - ${exportPath}: Local path where the repository data will be exported to.
    ...    - ${revision}: Revision number.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    ${_CURRENT_TIMESTAMP_}    Get Time    epoch
    Init Action    ${ACTION_CHECKOUT}
    Action Set    ${PRT_CHECKOUT_CVS_URI}    ${uri}
    Action Set    ${PRT_CHECKOUT_CVS_MODULE}    ${module}
    Action Set    ${PRT_CHECKOUT_CVS_PASSWORD}    ${password}
    Action Set    ${PRT_CHECKOUT_CVS_EXPORT_PATH}    ${_WORKING_DIR}${/}${exportPath}${/}${_CURRENT_TIMESTAMP_}
    Action Set    ${PRT_CHECKOUT_CVS_REVISION}    ${revision}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}
