*** Settings ***
Default Tags      win    ALL
Test Template     FWI Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK     USERNAME                       PASSWORD                       HOST                           DN NAME                           DSN                           PRODUCT ID    PROJECT ID    USER DIR                      DIR FILTER    ARGUMENTS    RETCODE    STATUS          REPORT MESSAGE
1.Successful with needed fields
                      OK         ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS       TEST_STR      ${EMPTY}                      ${EMPTY}      ${EMPTY}     0          ${ENDED_OK}     Operation completed

2.Successful with needed fields & User Dir
                      [Setup]    Prepare User Dir               TC2
                      OK         ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS       TEST_STR      ${_WORKING_DIR}/AT_FWI/TC2    ${EMPTY}      ${EMPTY}     0          ${ENDED_OK}     Operation completed

3.Successful with needed fields & User Dir, Dir Filter
                      [Setup]    Prepare User Dir               TC3
                      OK         ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS       TEST_STR      ${_WORKING_DIR}/AT_FWI/TC3    Documents     ${EMPTY}     0          ${ENDED_OK}     Operation completed

4.Successful with needed fields & User_Dir, Dir Filter, Optional arguments
                      [Setup]    Prepare User Dir               TC4
                      OK         ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS       TEST_STR      ${_WORKING_DIR}/AT_FWI/TC4    Documents     /TOUCH       0          ${ENDED_OK}     Operation completed

5.Fail with invalid user
                      NOK        ${_DIMENSIONS_USER}_invalid    ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS       TEST_STR      ${EMPTY}                      ${EMPTY}      ${EMPTY}     0          ${ENDED_NOK}    ${EMPTY}

6.Fail with invalid password
                      NOK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}_invalid    ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS       TEST_STR      ${EMPTY}                      ${EMPTY}      ${EMPTY}     0          ${ENDED_NOK}    ${EMPTY}

7.Fail with invalid host
                      NOK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}_invalid    ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS       TEST_STR      ${EMPTY}                      ${EMPTY}      ${EMPTY}     0          ${ENDED_NOK}    ${EMPTY}

8.Fail with invalid database name
                      NOK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}_invalid    ${_DIMENSIONS_DSN}            QLARIUS       TEST_STR      ${EMPTY}                      ${EMPTY}      ${EMPTY}     0          ${ENDED_NOK}    ${EMPTY}

9.Fail with invalid dsn
                      NOK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}_invalid    QLARIUS       TEST_STR      ${EMPTY}                      ${EMPTY}      ${EMPTY}     0          ${ENDED_NOK}    ${EMPTY}

*** Keywords ***
FWI Template
    [Arguments]    ${OK_NOK}    ${user}    ${pass}    ${host}    ${dbname}    ${dsn}
    ...    ${product_id}    ${project_id}    ${user_dir}    ${dir_filter}    ${args}    ${returnCode}
    ...    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_DIMENSIONS.PUB.ACTION.FWI with inputs
    ...    - ${user}: Username to access Serena Dimensions CM.
    ...    - ${pass}: Password to access Serena Dimensions CM.
    ...    - ${host}: Serena Dimensions CM hostname
    ...    - ${dbname}: Specifies the Serena Dimensions CM database identifier.
    ...    - ${dsn}: Specifies the data source name for connecting to the database.
    ...    - ${product_id}: Product ID
    ...    - ${project_id}: Project ID
    ...    - ${user_dir}: Specifies the directory to which items are to be fetched. If not set the user's default working area will be used.
    ...    - ${dir_filter}: Specifies the directory to which items are to be fetched.
    ...    - ${args}: Can be used to specify any optional arguments for FWI.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_FWI}
    Action Set    ${PRT_DIMENSIONS_USERNAME}    ${user}
    Action Set    ${PRT_DIMENSIONS_PASSWORD}    ${pass}
    Action Set    ${PRT_DIMENSIONS_HOST}    ${host}
    Action Set    ${PRT_DIMENSIONS_DBNAME}    ${dbname}
    Action Set    ${PRT_DIMENSIONS_DSN}    ${dsn}
    Action Set    ${PRT_FWI_DIMENSIONS_PRODUCT_ID}    ${product_id}
    Action Set    ${PRT_FWI_DIMENSIONS_PROJECT_ID}    ${project_id}
    Action Set    ${PRT_FWI_DIMENSIONS_USER_DIR}    ${user_dir}
    Action Set    ${PRT_FWI_DIMENSIONS_DIR_FILTER}    ${dir_filter}
    Action Set    ${PRT_FWI_DIMENSIONS_ARGS}    ${args}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}

Prepare User Dir
    [Arguments]    ${dir_name}
    Delete Dir    ${_WORKING_DIR}/AT_FWI/${dir_name}
    Create Dir    ${_WORKING_DIR}/AT_FWI/${dir_name}
