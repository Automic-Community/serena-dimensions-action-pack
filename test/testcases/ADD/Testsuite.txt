*** Settings ***
Documentation     In order to execute this test, ${_CVS_BASE_MODULE} should be available and valid
Suite Setup       Setup ADD testsuite
Default Tags      win    unix
Test Template     Add Template
Resource          ../../resources/keywords.txt

*** Variables ***
${add_working_dir}    ${_WORKING_DIR}${/}ADD_TEST
${add_test_module}    ${_CVS_BASE_MODULE}

*** Test Cases ***    OK_NOK     URI                       PASSWORD                   WORKING COPY                 TARGET           MESSAGE                                      MODULE                       RETCODE    STATUS          REPORT MESSAGE
1.Successful add file and folder
                      [Setup]    Setup ADD testcase        TC01                       test.txt
                      OK         ${_CVS_URI}               ${_CVS_PASS}               ${add_working_dir}           TC01             add file to repository - not commit yet-1    ${add_test_module}           0          ${ENDED_OK}     TC01 added to the repository
                      OK         ${_CVS_URI}               ${_CVS_PASS}               ${add_working_dir}           TC01/test.txt    add file to repository - not commit yet-1    ${add_test_module}           0          ${ENDED_OK}     cvs add: use 'cvs commit' to add this file permanently

2.Fail with invalid working copy path
                      [Setup]    Setup ADD testcase        TC02                       ${EMPTY}
                      NOK        ${_CVS_URI}               ${_CVS_PASS}               ${add_working_dir}invalid    TC02             add file to repository - not commit yet-2    ${add_test_module}           0          ${ENDED_NOK}    ERROR[Directory]

3.Fail with invalid target path
                      NOK        ${_CVS_URI}               ${_CVS_PASS}               ${add_working_dir}           TC03             add file to repository - not commit yet-3    ${add_test_module}           0          ${ENDED_NOK}    cvs add: nothing known about TC03

4.Fail with invalid module
                      [Setup]    Setup ADD testcase        TC04                       ${EMPTY}
                      NOK        ${_CVS_URI}               ${_CVS_PASS}               ${add_working_dir}           TC04             add file to repository - not commit yet-4    ${add_test_module}invalid    0          ${ENDED_NOK}    ${EMPTY}

5.Fail with invalid uri
                      [Setup]    Setup ADD testcase        TC05                       ${EMPTY}
                      NOK        ${_CVS_URI}_invaliduri    ${_CVS_PASS}               ${add_working_dir}           TC05             add file to repository - not commit yet-5    ${add_test_module}           0          ${ENDED_NOK}    no such repository

6.Fail with invalid password
                      [Setup]    Setup ADD testcase        TC06                       ${EMPTY}
                      NOK        ${_CVS_URI}               ${_CVS_PASS}_invalidcvs    ${add_working_dir}           TC06             add file to repository - not commit yet-6    ${add_test_module}           0          ${ENDED_NOK}    authorization failed

7.Faild add existed dir
                      [Setup]    Setup ADD testcase        TC07                       ${EMPTY}
                      OK         ${_CVS_URI}               ${_CVS_PASS}               ${add_working_dir}           TC07             add file to repository - not commit yet-1    ${add_test_module}           0          ${ENDED_OK}     ${EMPTY}
                      NOK        ${_CVS_URI}               ${_CVS_PASS}               ${add_working_dir}           TC07             add file to repository - not commit yet-1    ${add_test_module}           0          ${ENDED_NOK}    TC07 has already been entered

*** Keywords ***
Setup ADD testsuite
    [Documentation]    Setup steps before execute test
    Log    Setup for testing ADD action
    Log    Try to delete created folder from last execution
    Delete Dir    ${add_working_dir}
    Create Dir    ${add_working_dir}
    Log    Checkout existed module '${add_test_module}'
    Checkout Module    ${add_test_module}    ${EMPTY}    ${add_working_dir}
    Update Working Dir    ${add_working_dir}
    Log    Finish setup for    ADD testsuite

Setup ADD testcase
    [Arguments]    ${dir}    ${file}
    Delete Dir    ${add_working_dir}${/}${add_test_module}${/}${dir}
    Create File    ${add_working_dir}${/}${add_test_module}${/}${dir}${/}${file}

Add Template
    [Arguments]    ${OK_NOK}    ${uri}    ${password}    ${working_copy}    ${target}    ${message}
    ...    ${module}    ${returnCode}    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_CVS.PUB.ACTION.ADD with inputs
    ...    - ${uri}: Repository URI. Example: ":pserver:username@cvs.server.local:/usr/local/cvs".
    ...    - ${password}: Password used for authentication with the repository.
    ...    - ${working_copy}: Local working copy.
    ...    - ${target}: Path to the file or directory that should be added. Path must be relaitve to Working Copy
    ...    - ${module}: Module name.
    ...    - ${message}: Log message.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_ADD}
    Action Set    ${PRT_ADD_CVS_URI}    ${uri}
    Action Set    ${PRT_ADD_CVS_PASSWORD}    ${password}
    Action Set    ${PRT_ADD_CVS_WORKING_DIRECTORY}    ${working_copy}
    Action Set    ${PRT_ADD_CVS_TARGET_PATH}    ${target}
    Action Set    ${PRT_ADD_CVS_MESSAGE}    ${message}
    Action Set    ${PRT_ADD_CVS_MODULE}    ${module}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}
