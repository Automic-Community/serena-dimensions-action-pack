*** Settings ***
Default Tags      win
Test Template     CS Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK    USERNAME                       PASSWORD                       HOST                           DB_NAME                           DSN                           STREAM_ID                  DESCRIPTION                          BASE_STREAM             BASELINE                  ARGS                   RETCODE    STATUS          REPORT MESSAGE
1.Successful          OK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            ${_DIMENSION_STREAM_ID}    1.Successful                         ${_DIMENSION_STREAM}    ${_DIMENSION_BASELINE}    /PRODUCT=AT_PRODUCT    0          ${ENDED_OK}     ${EMPTY}

2.Fail with invalid user
                      NOK       ${_DIMENSIONS_USER}_invalid    ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            ${_DIMENSION_STREAM_ID}    2.Fail with invalid user             ${_DIMENSION_STREAM}    ${_DIMENSION_BASELINE}    /PRODUCT=AT_PRODUCT    0          ${ENDED_NOK}    ${EMPTY}

3.Fail with invalid password
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}_invalid    ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            ${_DIMENSION_STREAM_ID}    3.Fail with invalid password         ${_DIMENSION_STREAM}    ${_DIMENSION_BASELINE}    /PRODUCT=AT_PRODUCT    0          ${ENDED_NOK}    ${EMPTY}

4.Fail with invalid host
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}_invalid    ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            ${_DIMENSION_STREAM_ID}    4.Fail with invalid host             ${_DIMENSION_STREAM}    ${_DIMENSION_BASELINE}    /PRODUCT=AT_PRODUCT    0          ${ENDED_NOK}    ${EMPTY}

5.Fail with invalid database name
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}_invalid    ${_DIMENSIONS_DSN}            ${_DIMENSION_STREAM_ID}    5.Fail with invalid database name    ${_DIMENSION_STREAM}    ${_DIMENSION_BASELINE}    /PRODUCT=AT_PRODUCT    0          ${ENDED_NOK}    ${EMPTY}

6.Fail with invalid dsn
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}_invalid    ${_DIMENSION_STREAM_ID}    6.Fail with invalid dsn              ${_DIMENSION_STREAM}    ${_DIMENSION_BASELINE}    /PRODUCT=AT_PRODUCT    0          ${ENDED_NOK}    ${EMPTY}

*** Keywords ***
CS Template
    [Arguments]    ${OK_NOK}    ${user}    ${pass}    ${host}    ${dbname}    ${dsn}
    ...    ${stream_id}    ${description}    ${stream}    ${baseline}    ${args}    ${returnCode}
    ...    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_DIMENSIONS.PUB.ACTION.CS with inputs
    ...    - ${user}: Username to access Serena Dimensions CM.
    ...    - ${pass}: Password to access Serena Dimensions CM.
    ...    - ${host}: Serena Dimensions CM hostname
    ...    - ${dbname}: Specifies the Serena Dimensions CM database identifier.
    ...    - ${dsn}: Specifies the data source name for connecting to the database.
    ...    - ${stream_id}: Specifies the name of stream.
    ...    - ${description}: Specifies a description for the stream.
    ...    - ${stream}: Optionally specifies the stream on which to base the new stream.
    ...    - ${baseline}: Optionally specifies the Dimensions baseline on which to base the new stream.
    ...    - ${args}: Optionally specify any optional arguments for CS.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_CS}
    Action Set    ${PRT_DIMENSIONS_USERNAME}    ${user}
    Action Set    ${PRT_DIMENSIONS_PASSWORD}    ${pass}
    Action Set    ${PRT_DIMENSIONS_HOST}    ${host}
    Action Set    ${PRT_DIMENSIONS_DBNAME}    ${dbname}
    Action Set    ${PRT_DIMENSIONS_DSN}    ${dsn}
    Action Set    ${PRT_CS_DIMENSIONS_STREAM_ID}    ${stream_id}
    Action Set    ${PRT_CS_DIMENSIONS_DESCRIPTION}    ${description}
    Action Set    ${PRT_CS_DIMENSIONS_STREAM}    ${stream}
    Action Set    ${PRT_CS_DIMENSIONS_BASELINE}    ${baseline}
    Action Set    ${PRT_CS_DIMENSIONS_ARGS}    ${args}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}
