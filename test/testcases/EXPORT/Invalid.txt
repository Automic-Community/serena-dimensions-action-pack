*** Settings ***
Suite Setup       Suite Setup
Default Tags      win    unix
Test Template     Export Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    URI                                           MODULE            PASSWORD        REVISION    EXPORT_PATH
Invalid Password      ${_CVS_URI}                                   ${_CVS_MODULE}    invalid         HEAD        ${_WORKING_DIR}${/}Export${/}InvalidPassword

Invalid Uri           :pserver:@cvs@vvnfibocvs01:/usr/local/repo    ${_CVS_MODULE}    ${_CVS_PASS}    HEAD        ${_WORKING_DIR}${/}Export${/}InvalidUri

Invalid Host          :pserver:cvs@vvnfibocvs99:/usr/local/repo     ${_CVS_MODULE}    ${_CVS_PASS}    HEAD        ${_WORKING_DIR}${/}Export${/}InvalidHost

Invalid Revision      ${_CVS_URI}                                   ${_CVS_MODULE}    ${_CVS_PASS}    999.9       ${_WORKING_DIR}${/}Export${/}InvalidRevision

Invalid Module        ${_CVS_URI}                                   WrongModule       ${_CVS_PASS}    HEAD        ${_WORKING_DIR}${/}Export${/}InvalidModule

*** Keywords ***
Suite Setup
    log    "Suite Setup"

Export Template
    [Arguments]    ${uri}    ${module}    ${password}    ${revision}    ${path}
    Delete Directory    ${path}
    Init Action    ${ACTION_EXPORT}
    Action Set    ${PRT_EXPORT_CVS_URI}    ${uri}
    Action Set    ${PRT_EXPORT_CVS_MODULE}    ${module}
    Action Set    ${PRT_EXPORT_CVS_PASSWORD}    ${password}
    Action Set    ${PRT_EXPORT_CVS_EXPORT_PATH}    ${path}
    Action Set    ${PRT_EXPORT_CVS_REVISION}    ${revision}
    Action Execute
    Action Return Code Should Be    1
    Action Return Status Should Be    ${ENDED_NOK}
    Action Report Should Be Found
