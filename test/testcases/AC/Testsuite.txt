*** Settings ***
Default Tags      win    ALL
Test Template     AC Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK    USERNAME                       PASSWORD                       HOST                           DN NAME                           DSN                           REQUEST ID                 STATUS               CHECK       COMMENT    RETCODE    STATUS          REPORT MESSAGE
1.Successful action request with valid parameters_ACTION
                      OK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_13            ${EMPTY}             ACTION      TEST       0          ${ENDED_OK}     Operation completed

1+.Successful action request with valid parameters_NOCHECK
                      OK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_15            Under Work           NO CHECK    TEST       0          ${ENDED_OK}     Operation completed
                      OK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_15            Peer Review          NO CHECK    TEST       0          ${ENDED_OK}     Operation completed
                      OK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_15            Closed               NO CHECK    TEST       0          ${ENDED_OK}     Operation completed
                      OK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_15            Raised               NO CHECK    TEST       0          ${ENDED_OK}     Operation completed

2.Successful action request - closure check with valid parameters_CLOSURE
                      OK        ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_13            ${EMPTY}             CLOSURE     TEST       0          ${ENDED_OK}     Operation completed

3.Fail with invalid user
                      NOK       ${_DIMENSIONS_USER}_invalid    ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_13            IN TEST              NO CHECK    TEST       0          ${ENDED_NOK}    ${EMPTY}

4.Fail with invalid password
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}_invalid    ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_13            IN TEST              NO CHECK    TEST       0          ${ENDED_NOK}    ${EMPTY}

5.Fail with invalid host
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}_invalid    ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_13            IN TEST              NO CHECK    TEST       0          ${ENDED_NOK}    ${EMPTY}

6.Fail with invalid database name
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}_invalid    ${_DIMENSIONS_DSN}            QLARIUS_TASK_13            IN TEST              NO CHECK    TEST       0          ${ENDED_NOK}    ${EMPTY}

7.Fail with invalid dsn
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}_invalid    QLARIUS_TASK_13            IN TEST              NO CHECK    TEST       0          ${ENDED_NOK}    ${EMPTY}

8.Fail with invalid request id
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_13_invalid    IN TEST              NO CHECK    TEST       0          ${ENDED_NOK}    ${EMPTY}

9.Fail with invalid status
                      NOK       ${_DIMENSIONS_USER}            ${_DIMENSIONS_PASS}            ${_DIMENSIONS_HOST}            ${_DIMENSIONS_DB_NAME}            ${_DIMENSIONS_DSN}            QLARIUS_TASK_13            NO_STATUS_Invalid    NO CHECK    TEST       0          ${ENDED_NOK}    ${EMPTY}

*** Keywords ***
AC Template
    [Arguments]    ${OK_NOK}    ${user}    ${pass}    ${host}    ${dbname}    ${dsn}
    ...    ${requestId}    ${status}    ${check}    ${comment}    ${returnCode}    ${returnStatus}
    ...    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_DIMENSIONS.PUB.ACTION.AC with inputs
    ...    - ${user}: Username to access Serena Dimensions CM.
    ...    - ${pass}: Password to access Serena Dimensions CM.
    ...    - ${host}: Serena Dimensions CM hostname
    ...    - ${dbname}: Specifies the Serena Dimensions CM database identifier.
    ...    - ${dsn}: message should be contained in execution report
    ...    - ${requestId}: Specifies the Request Id.
    ...    - ${status}: Specifies next status.
    ...    - ${check}: Specifies check action request.
    ...    - ${comment}: Specifies comment of user when change status.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_AC}
    Action Set    ${PRT_DIMENSIONS_USERNAME}    ${user}
    Action Set    ${PRT_DIMENSIONS_PASSWORD}    ${pass}
    Action Set    ${PRT_DIMENSIONS_HOST}    ${host}
    Action Set    ${PRT_DIMENSIONS_DBNAME}    ${dbname}
    Action Set    ${PRT_DIMENSIONS_DSN}    ${dsn}
    Action Set    ${PRT_FWI_DIMENSIONS_AC_REQUEST_ID}    ${requestId}
    Action Set    ${PRT_FWI_DIMENSIONS_AC_STATUS}    ${status}
    Action Set    ${PRT_FWI_DIMENSIONS_AC_CHECK}    ${check}
    Action Set    ${PRT_FWI_DIMENSIONS_AC_COMMENT}    ${comment}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}
